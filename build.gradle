apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'fr.univ-nantes.julestar'
archivesBaseName = 'uima-tree-tagger-wrapper'
version = '1.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

if(hasProperty("signing.keyId")) {
	// activate signing of artifacts, only if signing is configured properly
	signing {
		sign configurations.archives
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}
dependencies {
	compile 'org.apache.uima:uimaj-core:2.6.0'
	compile "org.apache.uima:uimafit-core:2.1.0"
	compile 'org.annolab.tt4j:org.annolab.tt4j:1.1.1' // TT4J
	compile 'fr.univ-nantes.julestar:uima-profiler:1.0'
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId group
			artifactId archivesBaseName
			version version

			from components.java
		}
	}
}

if(hasProperty("ossrhUsername")) {
	uploadArchives {
		repositories {
		  mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			  authentication(userName: ossrhUsername, password: ossrhPassword)
			}
	
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
			  authentication(userName: ossrhUsername, password: ossrhPassword)
			}
	
			pom.project {
			  name 'UIMA TreeTagger wrapper'
			  packaging 'jar'
			  // optionally artifactId can be defined here
			  description ' A UIMA Analysis Engine wrapper for TreeTagger based on tt4j'
			  url 'https://github.com/JuleStar/uima-tree-tagger-wrapper'
	
			  scm {
				  connection 'scm:git:git@github.com:julestar/uima-tree-tagger-wrapper.git'
				  developerConnection 'scm:git:git@github.com:julestar/uima-tree-tagger-wrapper.git'
				  url 'git@github.com:julestar/uima-tree-tagger-wrapper.git'
			  }
	
			  licenses {
				  license {
					  name 'The Apache License, Version 2.0'
					  url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				  }
			  }
	
			  developers {
				  developer {
					  id developerId
					  name developerName
					  email developerEmail
				  }
			  }
			}
		  }
		}
	}
} else {
def msg = "Publishing to maven central (task uploadArchives) is not possible since no gradle property file is found (use \"\$ cp gradle.properties.sample gradle.properties\")"
logger.warn(msg)
}